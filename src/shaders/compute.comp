#version 460

layout(local_size_x = 8, local_size_y = 8, local_size_z = 1) in;

layout(set = 0, binding = 0, rgba8) uniform writeonly image2D img;

void main() {
    vec2 norm_coordinates = (gl_GlobalInvocationID.xy + vec2(0.5)) / vec2(imageSize(img));

    vec2 c = vec2(
        mix(-2.5, 1.0, norm_coordinates.x),
        mix(-1.5, 1.5, norm_coordinates.y)
    );

    vec2 z = vec2(0.0, 0.0);
    float i;
    const float max_iter = 200.0;
    for (i = 0.0; i < max_iter; i++) {
        z = vec2(
            z.x * z.x - z.y * z.y + c.x,
            2.0 * z.x * z.y + c.y
        );

        if (dot(z, z) > 4.0) {
            break;
        }
    }

    float t = i / max_iter;
    vec4 to_write = vec4(vec3(t), 1.0);
    imageStore(img, ivec2(gl_GlobalInvocationID.xy), to_write);
}
